apply plugin: 'com.android.library'
apply plugin: 'maven'
apply from: "../versions.gradle"
/**
 * MaterialSearchView helps you add material-themed searching to your app with ease.
 * <p>
 * Copyright 2017 Michael Garner (mgarnerdev)
 * <p>
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * <p>
 * http://www.apache.org/licenses/LICENSE-2.0
 * <p>
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Update build process for convenience. Assemble readme file. Update versions. Apply apache license to a couple more files.
 */

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "${versions.artifactId}-${versions.versionName}")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}.aar"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:${versions.supportLibrary}"
    compile "com.android.support:cardview-v7:${versions.supportLibrary}"
    compile "com.android.support:recyclerview-v7:${versions.supportLibrary}"
}

task createPom {
    doLast {
        pom {
            project {
                groupId "${versions.groupId}"
                artifactId "${versions.artifactId}"
                version "${versions.versionName}"

                inceptionYear "${versions.inceptionYear}"
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo("$buildDir/outputs/aar/${versions.artifactId}-${versions.versionName}.pom")
    }
}
